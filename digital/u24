!!!!    6    0    1 1652321663  V361b                                         
sequential

vector cycle 10u
receive delay 8u

! assign EPAD to pins 21
! power  EPAD

default device "u24"
set terminators to on
assign VCC  to pins *
assign GND  to pins *,*

assign NC   to pins *,*,*,*,*
assign NC   to pins *,*,*,*
assign NC   to pins *,*

assign RSTB to pins 4
assign ADDR to pins 3,14,2  ! A2,A1,A0
assign SDA  to pins 13
assign SCL  to pins 12
assign MODE to pins * default "0"

power   VCC,GND
inputs  RSTB,SCL,ADDR,MODE
bidirectional SDA
nondigital NC

family LVT

vector INIT1
 drive SDA
 set RSTB to "0"
 set ADDR to "xxx"
 set SDA  to "1"
 set SCL  to "1"
end vector

vector KEEP1
 receive SDA
 set RSTB to "k"
 set ADDR to "kkk"
 set SDA  to "x"
 set SCL  to "k"
end vector

vector KEEP2
 drive SDA
 set RSTB to "k"
 set ADDR to "kkk"
 set SDA  to "k"
 set SCL  to "k"
end vector

vector RSTB_0
 initialize to KEEP1
 set RSTB   to "0"
end vector

vector RSTB_1
 initialize to KEEP1
 set RSTB   to "1"
end vector

vector A_000
 initialize to KEEP1
 set ADDR   to "000"
end vector

vector A_001
 initialize to KEEP1
 set ADDR   to "001"
end vector

vector A_010
 initialize to KEEP1
 set ADDR   to "010"
end vector

vector A_011
 initialize to KEEP1
 set ADDR   to "011"
end vector

vector A_100
 initialize to KEEP1
 set ADDR   to "100"
end vector

vector A_101
 initialize to KEEP1
 set ADDR   to "101"
end vector

vector A_110
 initialize to KEEP1
 set ADDR   to "110"
end vector

vector A_111
 initialize to KEEP1
 set ADDR   to "111"
end vector

vector SK_10
 initialize to KEEP1
 set SCL    to "0"
end vector

vector SK_11
 initialize to KEEP1
 set SCL    to "1"
end vector

vector SK_20
 initialize to KEEP2
 set SCL    to "0"
end vector

vector SK_21
 initialize to KEEP2
 set SCL    to "1"
end vector

vector R_0
 initialize to KEEP1
 set SDA    to "0"
end vector

vector R_1
 initialize to KEEP1
 set SDA    to "1"
end vector

vector D_0
 initialize to KEEP2
 set SDA    to "0"
end vector

vector D_1
 initialize to KEEP2
 set SDA    to "1"
end vector

! ****************************************************************************

sub RST_IC
 repeat 40 times
  execute RSTB_0
 end repeat
 repeat 40 times
  execute RSTB_1
 end repeat
 wait 1600m
end sub

sub IDLE1
 repeat 20 times
  execute KEEP1
 end repeat
end sub

sub STRT
 execute D_1
 execute D_1
 execute SK_21
 execute SK_21
 execute D_0
 execute D_0
 execute SK_20
 execute SK_20
end sub

sub STOP
 execute SK_20
 execute SK_20
 execute D_0
 execute D_0
 execute SK_21
 execute SK_21
 execute D_1
 execute D_1
 repeat 20 times
  execute KEEP2
 end repeat
end sub

sub WR(D7,D6,D5,D4,D3,D2,D1,D0)
 execute D7
 execute SK_21
 execute SK_20
 execute D6
 execute SK_21
 execute SK_20
 execute D5
 execute SK_21
 execute SK_20
 execute D4
 execute SK_21
 execute SK_20
 execute D3
 execute SK_21
 execute SK_20
 execute D2
 execute SK_21
 execute SK_20
 execute D1
 execute SK_21
 execute SK_20
 execute D0
 execute SK_21
 execute SK_10
 execute R_0    ! Acknowledge bit
 execute SK_11
 execute SK_10
 call IDLE1
end sub

sub WX(D7,D6,D5,D4,D3,D2,D1,D0)  ! Acknowledge bit not active
 execute D7
 execute SK_21
 execute SK_20
 execute D6
 execute SK_21
 execute SK_20
 execute D5
 execute SK_21
 execute SK_20
 execute D4
 execute SK_21
 execute SK_20
 execute D3
 execute SK_21
 execute SK_20
 execute D2
 execute SK_21
 execute SK_20
 execute D1
 execute SK_21
 execute SK_20
 execute D0
 execute SK_21
 execute SK_10
 execute R_1    ! Acknowledge bit
 execute SK_11
 execute SK_10
 call IDLE1
end sub

! ******************************** ABUS = 010 ********************************
unit "Device Address = 010"
 execute INIT1
 execute A_010
 call RST_IC
 call STRT
 call WR(D_1,D_1,D_1,D_0, D_0,D_1,D_0,D_0)  ! Device address match
 call STOP
 call STRT
 call WX(D_1,D_1,D_1,D_0, D_1,D_0,D_1,D_0)  ! Device address not match
 call STOP
 repeat 40 times                            ! Test pin RSTB stuck high
  execute RSTB_0
 end repeat
 call STRT
 call WX(D_1,D_1,D_1,D_0, D_0,D_1,D_0,D_0)  ! Acknowledge bit not active
 call STOP
end unit

! ******************************** ABUS = 101 ********************************

unit "Device Address = 101"
 execute INIT1
 execute A_101
 call RST_IC
 call STRT
 call WR(D_1,D_1,D_1,D_0, D_1,D_0,D_1,D_0)  ! Device address match
 call STOP
 call STRT
 call WX(D_1,D_1,D_1,D_0, D_0,D_1,D_0,D_0)  ! Device address not match
 call STOP
 repeat 40 times                            ! Test pin RSTB stuck high
  execute RSTB_0
 end repeat
 call STRT
 call WX(D_1,D_1,D_1,D_0, D_1,D_0,D_1,D_0)  ! Acknowledge bit not active
 call STOP
end unit


